//	disabledraw;

//見出しレベル取得
	menu "h1","h2","h3","h4","h5","h6";
	#ret = result;
	
	if( #ret == 0 ) endmacro;

//選択状態の判断～タグ挿入

	//選択状態の場合、選択範囲の前後にタグ挿入
	if( selecting == 1 ){
		//選択範囲を切り取り、配列変数に格納
		cut;
		beginclipboardread;
		#i = 0;
		$a[#i] = getclipboard;
		while( $a[#i] != "" ){
			#i = #i + 1;
			$a[#i] = getclipboard;
		}
		
		//配列変数に格納した文字列を結合して $OUT に格納
		$OUT = "";
		#j = 0;
		while( #i > #j ){
			$OUT = $OUT + $a[#j];
			#j = #j + 1;
		}
		
		//タグ挿入
		if( rightstr($OUT,1) == "\n" ){
			// $OUT の末尾が改行の場合
			$OUT = "<h" + str(#ret) + ">" + leftstr($OUT, strlen($OUT) - 1) + "</h" + str(#ret) + ">\n";
			insert $OUT;
		}
		else {
			// $OUT の末尾が改行でない場合
			$OUT = "<h" + str(#ret) + ">" + $OUT + "</h" + str(#ret) + ">";
			insert $OUT;
		}
		
		//クリップボード履歴をひとつ戻す
		getcliphist 1;
	}
	//非選択状態の場合、カーソル位置にタグ挿入
	else {
		insert "<h" + str(#ret) + "></h" + str(#ret) + ">\n";
		left 6;
	}

//	enabledraw;
	endmacro;
